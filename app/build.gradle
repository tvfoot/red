apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'net.ltgt.errorprone'
apply from: '../config/quality/quality.gradle'

android {
  compileSdkVersion rootProject.ext.config.compileSdkVersion
  buildToolsVersion rootProject.ext.config.buildToolsVersion
  defaultConfig {
    applicationId rootProject.ext.config.applicationId
    minSdkVersion rootProject.ext.config.minSdkVersion
    targetSdkVersion rootProject.ext.config.targetSdkVersion
    versionCode Integer.parseInt(System.getenv('CIRCLE_BUILD_NUM') ?: "153")
    versionName '1.0'
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    vectorDrawables {
      useSupportLibrary true
    }
  }

  signingConfigs {
    release {
      storeFile rootProject.file(System.getenv('KEYSTORE') ?: KEYSTORE)
      storePassword System.getenv('KEYSTORE_PASSWORD') ?: KEYSTORE_PASSWORD
      keyAlias System.getenv('KEY_ALIAS') ?: KEY_ALIAS
      keyPassword System.getenv('KEY_PASSWORD') ?: KEY_PASSWORD
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }

  dataBinding {
    enabled true
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  dexOptions {
    preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex', 'true'))
  }

  productFlavors {
    mock {
      applicationIdSuffix = ".mock"
    }
    prod {}
  }

  // Remove mockRelease as it's not needed.
  android.variantFilter { variant ->
    if (variant.buildType.name == 'release' && variant.getFlavors().
        get(0).name == 'mock') {
      variant.setIgnore(true);
    }
  }

  // Always show the result of every unit test, even if it passes.
  testOptions.unitTests {
    all {
      testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
      }
    }
    returnDefaultValues = true
  }

  lintOptions {
    warning 'InvalidPackage' // prevent error from references of non-Android package
  }
}

dependencies {
  compile "com.android.support:appcompat-v7:$rootProject.ext.lib.supportLibraryVersion"
  compile "com.android.support:support-annotations:$rootProject.ext.lib.supportLibraryVersion"
  compile "com.android.support:design:$rootProject.ext.lib.supportLibraryVersion"
  compile "com.android.support:recyclerview-v7:$rootProject.ext.lib.supportLibraryVersion"
  compile "com.android.support:design:$rootProject.ext.lib.supportLibraryVersion"
  compile "com.android.support:support-v4:$rootProject.ext.lib.supportLibraryVersion"
  compile "com.android.support.constraint:constraint-layout:$rootProject.ext.lib.constraintLayoutVersion"
  compile "com.jakewharton.timber:timber:$rootProject.ext.lib.timberVersion"
  compile "com.genius:groupie:$rootProject.ext.lib.groupieVersion"
  compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.lib.rxJavaVersion"
  compile "io.reactivex.rxjava2:rxandroid:$rootProject.ext.lib.rxAndroidVersion"
  compile "com.squareup.picasso:picasso:$rootProject.ext.lib.picassoVersion"
  compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:$rootProject.ext.lib.picassoOkHttp3DownloaderVersion"
  compile "com.squareup.okhttp3:okhttp:$rootProject.ext.lib.okHttpVersion"
  compile "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.lib.okHttpVersion"
  compile "com.squareup.retrofit2:retrofit:$rootProject.ext.lib.retrofitVersion"
  compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.lib.retrofitVersion"
  compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rootProject.ext.lib.retrofit2RxJava2Adapter"
  compile "com.google.code.gson:gson:$rootProject.ext.lib.gsonVersion"
  compile "com.google.dagger:dagger:$rootProject.ext.lib.daggerVersion"
  compile "com.google.firebase:firebase-core:$rootProject.ext.lib.firebaseVersion"
  compile "com.jakewharton.rxbinding2:rxbinding:$rootProject.ext.lib.rxBindingVersion"
  compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rootProject.ext.lib.rxBindingVersion"
  compile "com.jakewharton.rxbinding2:rxbinding-support-v4:$rootProject.ext.lib.rxBindingVersion"
  compile 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC2'
  debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.ext.lib.leakCanaryVersion"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.lib.leakCanaryVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.lib.daggerVersion"
  annotationProcessor "com.google.auto.value:auto-value:$rootProject.ext.lib.autoValueVersion"
  annotationProcessor "com.ryanharter.auto.value:auto-value-gson:$rootProject.ext.lib.autoValueGsonVersion"
  annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:$rootProject.ext.lib.autoValueParcelVersion"
  provided "com.google.code.findbugs:jsr305:$rootProject.ext.lib.jsr305Version"
  provided "com.google.auto.value:auto-value:$rootProject.ext.lib.autoValueVersion"
  provided "com.ryanharter.auto.value:auto-value-gson:$rootProject.ext.lib.autoValueGsonVersion"

  // JVM Tests
  testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.lib.leakCanaryVersion"
  testCompile "junit:junit:$rootProject.ext.lib.junitVersion"
  testCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.lib.okHttpVersion"
  testCompile "org.mockito:mockito-core:$rootProject.ext.lib.mockitoVersion"
  testCompile "com.squareup.retrofit2:retrofit-mock:$rootProject.ext.lib.retrofitVersion"
  testCompile "com.google.dagger:dagger:$rootProject.ext.lib.daggerVersion"
  testAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.lib.daggerVersion"

  androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.lib.espressoVersion"
  androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.ext.lib.espressoVersion"
  androidTestCompile "org.mockito:mockito-core:$rootProject.ext.lib.mockitoVersion"
  androidTestCompile "com.crittercism.dexmaker:dexmaker:$rootProject.ext.lib.dexmakerVersion"
  androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:$rootProject.ext.lib.dexmakerVersion"
  androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:$rootProject.ext.lib.dexmakerVersion"
  androidTestCompile "com.squareup.okhttp3:mockwebserver:$rootProject.ext.lib.okHttpVersion"
  androidTestCompile "com.squareup.retrofit2:retrofit-mock:$rootProject.ext.lib.retrofitVersion"
  androidTestCompile 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
  androidTestCompile "com.google.dagger:dagger:$rootProject.ext.lib.daggerVersion"
  androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.lib.daggerVersion"
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
  resolutionStrategy.force "com.android.support:support-annotations:$rootProject.ext.lib.supportLibraryVersion"
  resolutionStrategy.force "com.google.code.findbugs:jsr305:$rootProject.ext.lib.jsr305Version"
}

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
  println "Excluding compile dependency: ${compileDependency.getName()}"
  configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
    configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
  }
}

apply plugin: 'com.google.gms.google-services'